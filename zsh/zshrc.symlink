# Shortcut to this dotfiles
export DOTFILES=$HOME/.dotfiles

# Save secret crap inside ~/.localrc
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# All the zsh files
typeset -U config_files
config_files=($DOTFILES/*/*.zsh)

# First load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# Now, load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# Initialize autocomplete now
autoload -U compinit
compinit

# Load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

# We don't need the file list anymore
unset config_files

# Path
export PATH=$PATH:$DOTFILES/bin:$HOME/.local/bin

# Language
export LC_ALL=en_US.UTF-8
export LANG=en_US.UTF-8

